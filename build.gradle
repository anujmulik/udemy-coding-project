buildscript {
	ext {
		springBootVersion = '2.1.2.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id("com.github.node-gradle.node") version "2.2.0"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


group = 'com.avengers'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly group: 'org.postgresql', name: 'postgresql'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}


task copyWebApp(type: Copy) {
	from 'src/main/quizlet-ui/build'
	into 'build/resources/main/static/.'
}

task appNpmInstall(type: YarnTask) {
	description = "Installs all dependencies from package.json"
	workingDir = file("${project.projectDir}/src/main/quizlet-ui")
	args = ["install"]
}

task appNpmBuild(type: YarnTask) {
	description = "Builds production version of the quizlet-ui"
	workingDir = file("${project.projectDir}/src/main/quizlet-ui")
	args = ["run", "build"]
}

appNpmBuild.dependsOn appNpmInstall
copyWebApp.dependsOn appNpmBuild
compileJava.dependsOn copyWebApp

node {
	download = true
	version = "12.13.1"

	// Set the work directory for unpacking node
	workDir = file("${project.buildDir}/nodejs")

	// Set the work directory for NPM
	npmWorkDir = file("${project.buildDir}/npm")
}
